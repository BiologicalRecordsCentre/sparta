# JAGS code for SPARTA model plus random walk prior
# on the year effect of the state model + intercept + halfcauchy hyperpriors

# State model
for (i in 1:nsite){ 
  for (t in 1:nyear){   
    z[i,t] ~ dbern(muZ[i,t]) 
    logit(muZ[i,t])<- a[t] + eta[i] 
  }}   

# State model priors
a[1] ~ dnorm(0, 0.001)
for(t in 2:nyear){
  a[t] ~ dnorm(a[t-1], tau.a)
}

tau.a <- 1/(sd.a * sd.a)
sd.a ~ dt(0, 1, 1)T(0,) 
  
for (i in 1:nsite) {
  eta[i] ~ dnorm(0, tau2)       
} 

tau2 <- 1/(sigma2 * sigma2) 
sigma2 ~ dt(0, 1, 1)T(0,) 
  
# Observation model priors 
for (t in 1:nyear) {
  alpha.p[t] ~ dnorm(mu.lp, tau.lp)            
}

mu.lp ~ dnorm(0, 0.01)

tau.lp <- 1 / (sd.lp * sd.lp)                 
sd.lp ~ dt(0, 1, 1)T(0,)  
  
# Derived parameters
for (t in 1:nyear) {  
  psi.fs[t] <- sum(z[1:nsite, t])/nsite
}
